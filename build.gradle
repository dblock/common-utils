/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

buildscript {
    ext {
        opensearch_group = "com.github.dblock.OpenSearch"
        // https://jitpack.io/com/github/dblock/OpenSearch/6436867a003
        opensearch_version = '6436867a003'
        opendistroVersion = '1.15.0'
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }

    dependencies {
        classpath "${opensearch_group}:build-tools:${opensearch_version}"
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.diffplug.gradle.spotless" version "3.26.1"
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://plugins.gradle.org/m2/" }
    jcenter()
}

group 'com.amazon.opendistroforelasticsearch.commons'

sourceCompatibility = 10

version = "${opendistroVersion}.0"

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compileOnly "${opensearch_group}:opensearch:${opensearch_version}"
    compileOnly "${opensearch_group}:opensearch-rest-high-level-client:${opensearch_version}"
    compileOnly "${opensearch_group}:opensearch-x-content:${opensearch_version}"
    testCompile "${opensearch_group}:framework:${opensearch_version}"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'
}

spotless {
    java {
        removeUnusedImports()
        importOrder 'java', 'javax', 'org', 'com'
        licenseHeaderFile 'spotless.license.java'

        eclipse().configFile rootProject.file('.eclipseformat.xml')
    }
}

shadowJar {
    classifier = null
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        shadow(MavenPublication) {
            project.shadow.component(it)
            groupId = 'com.amazon.opendistroforelasticsearch'
            artifactId = 'common-utils'

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "Open Distro for OpenSearch Common Utils"
                packaging = "jar"
                url = "https://github.com/opendistro-for-elasticsearch/common-utils"
                description = "Open Distro for OpenSearch Common Utils"
                scm {
                    connection = "scm:git@github.com:opendistro-for-elasticsearch/common-utils.git"
                    developerConnection = "scm:git@github.com:opendistro-for-elasticsearch/common-utils.git"
                    url = "git@github.com:opendistro-for-elasticsearch/common-utils.git"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "amazonwebservices"
                        organization = "Amazon Web Services"
                        organizationUrl = "https://aws.amazon.com"
                    }
                }
            }
        }
    }

    gradle.startParameter.setShowStacktrace(ShowStacktrace.ALWAYS)
    gradle.startParameter.setLogLevel(LogLevel.DEBUG)

    signing {
        required { gradle.taskGraph.hasTask("publishShadowPublicationToSonatype-stagingRepository") }
        sign publishing.publications.shadow
    }
}
